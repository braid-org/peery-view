<!DOCTYPE html>
<html>
<head>
<meta charset=UTF-8>
<title>PeeryView</title>
<style>
    body {
        font-family: Lato, sans-serif;
        width: 100%;
        padding: 0;
        margin: 0;
    }
    .material-icons-outlined { user-select: none; }
    /* Rules for using icons as black on a light background. */
    .material-icons-outlined.md-dark { color: rgba(0, 0, 0, 0.54); }
    .material-icons-outlined.md-dark.md-inactive { color: rgba(0, 0, 0, 0.26); }

    /* Rules for using icons as white on a dark background. */
    .material-icons-outlined.md-light { color: rgba(255, 255, 255, 1); }
    .material-icons-outlined.md-light.md-inactive { color: rgba(255, 255, 255, 0.3); }

    .post-title:link {
        color: black;
    }
    .post-title:visited {
        color: #777;
    }
    .post-title:hover {
        color: #111;
    }
    .tooltip {
        width: -moz-fit-content;
        width: -webkit-fit-content;
        width: fit-content;
    }
    .hover-select:hover {
        background-color: #eee;
    }
    /* Hide scrollbar on webkit */
    .hide-scroll::-webkit-scrollbar {
        display: none;
    }
    .hide-scroll {
        -ms-overflow-style: none;
        scrollbar-width: none;
    }

</style>

<script type=coffee>
    window.outer_width = 800
    window.inner_width = 700
    window.slider_width = 220
    window.slider_height = (outer_width - inner_width) / 2 - 10

    window.unslash = (t) -> if t?.startsWith?("/") then t.substr(1) else t
    window.slash = (t) -> if t?.startsWith?("/") then t else "/#{t}"
    window.titlecase = (t) -> t.split(" ").map( (w) => w[0].toUpperCase() + w.substr 1).join(" ")
    
    window.change_path = (path) ->
        u = new URL window.location.href
        u.pathname = path
        history.pushState {
            url: u.pathname
            # we can write whatever other data we want in here...
        }, null, u

    window.parse_path = (path) ->
        path = slash unescape path
        if path.endsWith "/"
            path = path[...-1]
        v = bus.get "view"
        v.tag = v.user_key = v.page = null
        switch
            when path == "/users"
                v.page = "users"
                v.title = "Users"
            when path.startsWith "/@"
                v.user_key = path
                v.page = "posts"

                bus.get_once path, (user) ->
                    v.title = user.name
                    bus.set v
                    document.title = format_title "#{v.title}'s view"
            when path.startsWith "/post/"
                v.post_key = path
                v.page = "post_details"

                bus.get_once path, (post) ->
                    v.title = post.title
                    bus.set v

                    document.title = format_title v.title

            when path.length > 2
                v.tag = unslash path
                v.page = "posts"
                v.title = titlecase v.tag
            else
                v.page = "posts"
                v.title = null
        bus.set v
        document.title = format_title v.title

    window.load_path = (path) ->
        change_path path
        parse_path path

    window.format_title = (title) -> if title then "#{title} - PeeryView" else "PeeryView"

    window.addEventListener 'popstate', (e) ->
        popped = e.state
        if popped?
            parse_path popped.url
        else
            parse_path "/"
            

    parse_path ((new URL window.location.href).pathname)

    dom.BODY = ->
        v = bus.get "view"
        DIV
            key: "body-container"
            display: "flex"
            flexDirection: "column"
            alignItems: "center"
            
            HEADER
                key: "header"
            unless v?.page == "post_details"
                BIG_MULTIGRAM
                    key: "multigram"

            switch v?.page
                when "users" then USERS key: "users"
                when "post_details" then FULL_PAGE_POST key: "post", post: v.post_key
                else POSTS key: "posts"
            

    dom.BIG_MULTIGRAM = ->
        c = bus.get "/current_user"
        v = bus.get "view"

        username = v.user_key ? c?.user?.key ? "/@default" 
        params = 
            computed: true
            tag: v.tag
            untagged: !v.tag
        w = inner_width

        DIV
            width: w

            MULTIGRAM
                key: "multigram-inner"
                sldr: "#{username}/votes/people#{stringify_kson params}"
                width: w
                height: 130
                max_avatar_radius: 60
                read_only: (v.user_key) or !c.logged_in
                onsave: (vote) ->
                    # Wait, why do we need to do this on a copy?
                    copy = Object.assign {}, vote
                    copy.key = "#{username}/vote/#{unslash vote.target_key}"
                    copy.depth = 1
                    if v.tag?
                        copy.key = "#{copy.key}(tag:#{v.tag})"
                        copy.tag = v.tag
                    bus.set copy

    r = () ->
        parser = PPPParser bus
        parser("weights/@<userid>").to_get = (key, t) ->
            {userid} = t._path
            {tag, untagged} = t._params
            blob = stringify_kson {computed: true, tag: tag, untagged}

            votes = bus.get "/@#{userid}/votes/people#{blob}"
            weights_ret = {key: key}
            (votes.arr ? []).forEach (v) ->
                bus.get v
                weights_ret[v.target_key] = 2 * v.value - 1


            if tag
                ((bus.get "/@#{userid}/votes/people(computed,untagged)").arr ? []).forEach (v) ->
                    unless weights_ret[v.target_key]?
                        bus.get v
                        weights_ret[v.target_key] = 2 * v.value - 1

            weights_ret

        parser("score/post/<postid>").to_get = (key, t) ->
            {postid} = t._path
            {user, tag} = t._params

            user ?= "/@default"

            weights = bus.get "weights/#{unslash user}#{stringify_kson {tag}}"
                
            now = Date.now() / 1000

            kson = stringify_kson
                tag: tag
                untagged: !tag
        
            #// Subscribe to the post
            p = bus.get "/post/#{postid}"

            sum_votes = 0
            sum_weights = 0

            #// Subscribe to the post's votes
            (bus.get "/votes/#{unslash p.key}#{kson}")?.arr?.forEach (v) ->
                # first subscribe to the vote
                if v.key then bus.get v
                voter = v.user_key
                # Keep track of the total weight of votes, and of the weighted sum of votes.
                sum_weights += Math.abs(weights[voter] ? 0)
                sum_votes   += (2 * v.value - 1) * (weights[voter] ? 0)

            #// Our network-weight on the author
            author_weight = weights[p.user_key] ? 0

            res = compute_score
                age: now - p.time
                author: author_weight
                score: sum_votes
                volume: sum_weights

            {
                key: key,
                value: res
            }


    if document.readyState == "complete"
        r()
    else
        window.addEventListener("DOMContentLoaded", () => r())


</script>

<!-- React stuff -->
<script src="https://unpkg.com/react@17/umd/react.development.js" crossorigin></script>
<script src="https://unpkg.com/react-dom@17/umd/react-dom.development.js" crossorigin></script>
<script src="https://unpkg.com/create-react-class@15.7.0/create-react-class.min.js" crossorigin></script>
<script src='https://unpkg.com/babel-standalone@6/babel.min.js' crossorigin></script>

<!-- Main statebus script -->
<script src="/client.js" server="/"></script>

<!-- Configure the statebus -->
<script>
  // window.get = bus.get        // Apparently get and set are coffeescript
  // window.set = bus.set        // keywords.
  // window.fetch = bus.get      // And fetch() is now a web standard.
  // window.save = bus.set       // So there isn't as much value in making
  // window.delete = bus.delete  // any of these global variables anymore.
  window.loading = bus.loading
  bus.libs.http_out('/*', '/')
  bus.libs.react17.coffreact()
  bus.libs.localstorage('ls_*')
  window.state = bus.state       // But I think this is a nice global variable!
  bus.honk = 3
</script>

<!-- Load UI libraries -->
<script src="/static/vendor/considerit_shared.js" crossorigin=anonymous></script>
<script src="/coffee/parser.coffee" crossorigin=anonymous></script>
<script src="/coffee/ui.coffee" crossorigin=anonymous></script>
<script src="/coffee/post.coffee" crossorigin=anonymous></script>

<!-- For slidergrams -->
<script src="/coffee/avatar.coffee" crossorigin=anonymous></script>
<script src="/coffee/slidergrams.coffee" crossorigin=anonymous></script>
<script src="/coffee/multigrams.coffee" crossorigin=anonymous></script>

<script src="https://unpkg.com/blueimp-md5@2.19.0/js/md5.js"></script>
<script src="/static/vendor/quadtree.js"></script>

<!-- Google Fonts: Lato -->
<link rel="preconnect" href="https://fonts.googleapis.com">
<link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
<link href="https://fonts.googleapis.com/css2?family=Lato:wght@300;400;700&display=swap" rel="stylesheet">
<link href="https://fonts.googleapis.com/icon?family=Material+Icons+Outlined" rel="stylesheet">

<!-- Favicon -->
<link rel="icon" type="image/svg+xml" href="/static/favicon.svg" sizes="any">
</head>
</html>
