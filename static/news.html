<!DOCTYPE html>
<html lang=en>
<head>
<meta charset=UTF-8>
<meta name=viewport content="width=device-width, initial-scale=1">
<title>PeeryView</title>
<style>
    body {
        font-family: Lato, sans-serif;
        width: 100%;
        padding: 0;
        margin: 0;
        overflow-x: hidden;
    }
    /* unset some weird styles */
    p {
        margin: 0;
        padding: 0;
    }
    .unbutton {
        cursor: pointer;
        margin: 0;
        padding: 0;
        border: none;
        color: unset;
        display: inline-block;
        background: none;
        font-size: unset;
        box-shadow: none;
        text-decoration: none;
        text-align: left;
        -webkit-appearance: none;
        -moz-appearance: none;
    }
    .material-icons-outlined { user-select: none; }
    /* Rules for using icons as black on a light background. */
    .material-icons-outlined.md-dark { color: rgba(0, 0, 0, 0.54); }
    .material-icons-outlined.md-dark.md-inactive { color: rgba(0, 0, 0, 0.26); }

    /* Rules for using icons as white on a dark background. */
    .material-icons-outlined.md-light { color: rgba(255, 255, 255, 1); }
    .material-icons-outlined.md-light.md-inactive { color: rgba(255, 255, 255, 0.3); }

    .stylish-input {
        outline: none;
        -webkit-box-shadow: none;
        box-shadow: none;
        font-family: Lato;
        border: 1px solid rgba(0, 0, 0, 0.2);
    }
    .stylish-input:hover {
        border-color: rgba(0, 0, 50, 0.2);
    }
    .stylish-input:focus {
        border-color: rgba(10, 100, 200, 0.7);
    }

    .tooltip {
        width: -moz-fit-content;
        width: -webkit-fit-content;
        width: fit-content;
    }
    .hover-select:hover {
        background-color: #eee;
    }
    /* Hide scrollbar on webkit */
    .hide-scroll::-webkit-scrollbar {
        display: none;
    }
    .hide-scroll {
        -ms-overflow-style: none;
        scrollbar-width: none;
    }

    .mobile-show {
        display: none;
    }

    @media only screen and (max-width: 650px) {

        div[data-widget=POST] {
            width: 100vw;
        }
        .grid-slider {
            justify-self: center;
        }
        div[data-key=actual-header] {
            padding: 0 15px 8px 15px !important;
        }
        .mobile-hide {
            display: none;
        }
        .mobile-show {
            display: unset;
        }
        .mobile-rotate {
            transform: rotate(-90deg);
        }
        .x-of-y {
            max-width: calc(100vw - 80px);
        }
        .rolodex-entry {
            background: rgba(255, 255, 255, 0.8);
        }
        div[data-widget=POST], div[data-widget=USER] {
            margin-bottom: 1em !important;
        }
        div[data-key=lr-panels-container] {
            flex-direction: column !important;
        }
        span[data-key=tag-text] {
            font-size: 14px !important;
        }
    }

</style>

<script type=coffee>
    window.outer_width = 800
    window.inner_width = 700
    window.post_height = 45
    window.padding_unit = 10
    window.slider_width = 175
    window.slider_height = 40

    window.unslash = (t) -> if t?.startsWith?("/") then t.substr(1) else t
    window.slash = (t) -> if t?.startsWith?("/") then t else "/#{t}"
    window.titlecase = (t) -> t.split(" ").map( (w) => w[0].toUpperCase() + w.substr 1).join(" ")
    
    window.change_path = (path) ->
        u = new URL window.location.href
        u.pathname = path
        history.pushState {
            url: u.pathname
            # we can write whatever other data we want in here...
        }, null, u

    window.parse_path = (path) ->
        v = key: "view"

        path = slash unescape path
        if path.endsWith "/"
            path = path[...-1]
        if -1 != path.indexOf "\("
            [path, kson] = split_once path, "\("
            Object.assign v, parse_kson "\(#{kson}"
          
        switch
            # User list
            when path == "/users"
                v.page = "users"
                v.title = "Users"

            # User perspective
            when path.startsWith "/user/"
                v.user_key = path
                v.page = "posts"

                bus.fetch_once path, (user) ->
                    v.title = user.name
                    save v
                    document.title = format_title "#{v.title}'s view"

            # Post details
            when path.startsWith "/post/"
                v.post_key = path
                v.page = "post_details"

                bus.fetch_once path, (post) ->
                    v.title = post.title
                    save v

                    document.title = format_title v.title
            # Search view
            when path.startsWith "/search"
                v.page = "search"
                v.query = path[8..]

            # Tag view
            else
                v.page = "posts"
                v.title = null
                if v.tag
                    v.title = titlecase v.tag
        save v
        document.title = format_title v.title

    window.load_path = (path) ->
        change_path path
        parse_path path

    window.format_title = (title) -> if title then "#{title} - PeeryView" else "PeeryView"

    window.addEventListener 'popstate', (e) ->
        popped = e.state
        if popped?
            parse_path popped.url
        else
            parse_path "/"
    
    window.addEventListener 'click', (e) ->
        target = e.target ? e.srcElement
        if target.tagName != "A"
            target = target.closest "a"
        if target?.getAttribute?("data-load-intern") and !e.ctrlKey and !e.shiftKey
            e.preventDefault()
            # Use target.getAttribute instead of target.href to get the raw href
            load_path target.getAttribute "href"
            

    load = () ->
        path = new URL window.location.href
        parse_path path.pathname

    # wait until other scripts have loaded, so that we can use the KSON parser
    if document.readyState == "complete"
        load()
    else
        window.addEventListener("DOMContentLoaded", load)

    dom.BODY = ->
        v = fetch "view"
        DIV
            key: "body-container"
            display: "flex"
            flexDirection: "column"
            alignItems: "left"
            padding: 10

            IMG
                key: "peery-logo"
                className: "mobile-show"
                src: "/static/images/peeryicon.svg"
                height: "2em"
                marginTop: "1em"
            
            MAIN_HEADER
                key: "header"

            switch v?.page
                when "users" then [
                    BIG_MULTIGRAM key: "multigram"
                    USERS key: "users"
                    ]
                when "post_details" then [
                    BIG_MULTIGRAM key: "multigram"
                    FILTER key: "filter"

                    DIV
                        key: "tree-root"
                        marginLeft: 20
                        width: outer_width

                        if v.context then COLLAPSED_POST
                            key: "parent"
                            post: (fetch v.post_key).parent_key

                        POST
                            key: "post"
                            post: v.post_key
                            hide_reply: true
                            hide_focus: true

                    HOVER_REPLY
                        key: "reply"
                        parent: v.post_key
                        style: width: 600, marginLeft: 20

                    POSTS key: "posts", style: marginLeft: 20
                ]
                when "search" then [
                    SEARCH_BOX key: "search"
                    POSTS_SEARCH key: "posts"
                    ]

                else [
                    BIG_MULTIGRAM key: "multigram"
                    FILTER key: "filter"
                    SUBMIT_POST key: "submit"
                    POSTS key: "posts"
                    ]

    dom.BIG_MULTIGRAM = ->
        c = fetch "/current_user"
        v = fetch "view"

        username = v.user_key ? c?.user?.key ? "/user/default" 
        kson = stringify_kson
            computed: true
            tag: v.tag
            untagged: !v.tag

        w = inner_width

        DIV
            width: w
            className: "mobile-hide"
            marginLeft: 20

            MULTIGRAM
                key: "multigram-inner"
                sldr: "#{username}/votes/people#{kson}"
                width: w
                height: 130
                max_avatar_radius: 60
                read_only: (v.user_key) or !c.logged_in
                onsave: (vote) ->
                    # Wait, why do we need to do this on a copy?
                    copy = Object.assign {}, vote
                    copy.key = "#{username}/vote/#{unslash vote.target_key}"
                    copy.depth = 1
                    if v.tag?
                        copy.key = "#{copy.key}(tag:#{v.tag})"
                        copy.tag = v.tag
                    save copy



</script>

<!-- Main statebus script -->
<script src="/client.js" server="/"></script>
<script src="/static/vendor/considerit_shared.js" crossorigin=anonymous></script>
<script src="/coffee/parser.coffee" crossorigin=anonymous></script>
<script src="/coffee/ui.coffee" crossorigin=anonymous></script>
<script src="/coffee/post.coffee" crossorigin=anonymous></script>

<!-- For slidergrams -->
<script src="/coffee/avatar.coffee" crossorigin=anonymous></script>
<script src="/coffee/slidergrams.coffee" crossorigin=anonymous></script>
<script src="/coffee/multigrams.coffee" crossorigin=anonymous></script>

<script src="https://unpkg.com/blueimp-md5@2.19.0/js/md5.js"></script>
<script src="/static/vendor/quadtree.js"></script>

<!-- Google Fonts: Lato -->
<link rel="preconnect" href="https://fonts.googleapis.com">
<link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
<link href="https://fonts.googleapis.com/css2?family=Lato:wght@300;400;700&display=swap" rel="stylesheet">
<link href="https://fonts.googleapis.com/icon?family=Material+Icons+Outlined" rel="stylesheet">

<!-- Favicon -->
<link rel="icon" type="image/svg+xml" href="/static/favicon.svg" sizes="any">
</head>
</html>
