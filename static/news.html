<!DOCTYPE html>
<html lang=en>
<head>
<meta charset=UTF-8>
<meta name=viewport content="width=device-width, initial-scale=1">
<title>PeeryView</title>
<style>
    body {
        font-family: Lato, sans-serif;
        width: 100%;
        padding: 0;
        margin: 0;
        overflow-x: hidden;
    }
    /* unset some weird styles */
    p {
        margin: 0;
        padding: 0;
    }
    .unbutton {
        margin: 0;
        padding: 0;
        border: none;
        color: unset;
        display: inline-block;
        background: none;
        font-size: unset;
        box-shadow: none;
        text-decoration: none;
        text-align: left;
        -webkit-appearance: none;
        -moz-appearance: none;
    }
    .material-icons-outlined { user-select: none; }
    /* Rules for using icons as black on a light background. */
    .material-icons-outlined.md-dark { color: rgba(0, 0, 0, 0.54); }
    .material-icons-outlined.md-dark.md-inactive { color: rgba(0, 0, 0, 0.26); }

    /* Rules for using icons as white on a dark background. */
    .material-icons-outlined.md-light { color: rgba(255, 255, 255, 1); }
    .material-icons-outlined.md-light.md-inactive { color: rgba(255, 255, 255, 0.3); }

    .post-title:link {
        color: black;
    }
    .post-title:visited {
        color: #777;
    }
    .post-title:hover {
        color: #111;
    }
    .tooltip {
        width: -moz-fit-content;
        width: -webkit-fit-content;
        width: fit-content;
    }
    .hover-select:hover {
        background-color: #eee;
    }
    /* Hide scrollbar on webkit */
    .hide-scroll::-webkit-scrollbar {
        display: none;
    }
    .hide-scroll {
        -ms-overflow-style: none;
        scrollbar-width: none;
    }


    .post-title {
        font-size: 16px;
    }
    .mobile-show {
        display: none;
    }

    div[data-widget=POST] {
        width: min(800px, calc(100vw - 20ch));
    }
    div[data-widget=USER] {
        width: min(700px, calc(100vw - 20ch));
    }

    .post-main-grid {
        grid: "icon title       slider more" auto
              "icon domain_time slider more" 16px
            /  50px  1fr        225px  50px;
    }

    .user-main-grid {
        grid: "icon name       slider more" auto
              "icon joined     slider more" 16px
            /  50px  1fr        225px  50px;
    }
    
    @media only screen and (max-width: 650px) {

        div[data-widget=POST] {
            width: 100vw;
        }
        .post-main-grid {
            grid: "icon title more" auto
                  "icon domain_time more" 16px
                  "slider   slider slider" auto
                /  50px  1fr  30px;
            width: calc(100vw - 20px);
            margin: auto;
        }

        .user-main-grid {
            grid: "icon name       more" auto
                  "icon joined     more" 16px
                  "slider slider slider" auto
                /  50px  1fr       30px;
            width: min(700px, calc(100vw - 50px));
        }
        .grid-slider {
            justify-self: center;
        }
        div[data-key=actual-header] {
            padding: 0 15px 8px 15px !important;
        }
        .mobile-hide {
            display: none;
        }
        .mobile-show {
            display: unset;
        }
        .mobile-rotate {
            transform: rotate(-90deg);
        }
        .x-of-y {
            max-width: calc(100vw - 80px);
        }
        .rolodex-entry {
            background: rgba(255, 255, 255, 0.8);
        }
        div[data-widget=POST], div[data-widget=USER] {
            margin-bottom: 1em !important;
        }
        div[data-key=lr-panels-container] {
            flex-direction: column !important;
        }
        div[data-widget=POST_DETAILS] {
            margin: 10px 20px !important;
        }
        span[data-key=tag-text] {
            font-size: 14px !important;
        }
    }

</style>

<script type=coffee>
    window.outer_width = 800
    window.inner_width = 700
    window.slider_width = 225
    window.slider_height = 45

    window.unslash = (t) -> if t?.startsWith?("/") then t.substr(1) else t
    window.slash = (t) -> if t?.startsWith?("/") then t else "/#{t}"
    window.titlecase = (t) -> t.split(" ").map( (w) => w[0].toUpperCase() + w.substr 1).join(" ")
    
    window.change_path = (path) ->
        u = new URL window.location.href
        u.pathname = path
        history.pushState {
            url: u.pathname
            # we can write whatever other data we want in here...
        }, null, u

    window.parse_path = (path) ->
        path = slash unescape path
        if path.endsWith "/"
            path = path[...-1]
        v = fetch "view"
        v.tag = v.user_key = v.page = null
        switch
            # User list
            when path == "/users"
                v.page = "users"
                v.title = "Users"

            # User perspective
            when path.startsWith "/user/"
                v.user_key = path
                v.page = "posts"

                bus.fetch_once path, (user) ->
                    v.title = user.name
                    save v
                    document.title = format_title "#{v.title}'s view"

            # Post details
            when path.startsWith "/post/"
                v.post_key = path
                v.page = "post_details"

                bus.fetch_once path, (post) ->
                    v.title = post.title
                    save v

                    document.title = format_title v.title
            # Search view
            when path.startsWith "/search"
                v.page = "search"
                v.query = path[8..]

            # Tag view
            when path.length > 2
                v.tag = unslash path
                v.page = "posts"
                v.title = titlecase v.tag
            else
                v.page = "posts"
                v.title = null
        save v
        document.title = format_title v.title

    window.load_path = (path) ->
        change_path path
        parse_path path

    window.format_title = (title) -> if title then "#{title} - PeeryView" else "PeeryView"

    window.addEventListener 'popstate', (e) ->
        popped = e.state
        if popped?
            parse_path popped.url
        else
            parse_path "/"
    
    window.addEventListener 'click', (e) ->
        target = e.target ? e.srcElement
        if target.tagName != "A"
            target = target.closest "a"
        if target?.getAttribute?("data-load-intern") and !e.ctrlKey and !e.shiftKey
            e.preventDefault()
            # Use target.getAttribute instead of target.href to get the raw href
            load_path target.getAttribute "href"
            

    parse_path ((new URL window.location.href).pathname)

    dom.BODY = ->
        v = fetch "view"
        DIV
            key: "body-container"
            display: "flex"
            flexDirection: "column"
            alignItems: "center"

            IMG
                key: "peery-logo"
                className: "mobile-show"
                src: "/static/images/peeryicon.svg"
                height: "2em"
                marginTop: "1em"
            
            MAIN_HEADER
                key: "header"
            unless v?.page == "post_details" or v?.page == "search"
                BIG_MULTIGRAM
                    key: "multigram"

            if v?.page == "search"
                SEARCH_BOX
                    key: "search"

            switch v?.page
                when "users" then USERS key: "users"
                when "post_details" then FULL_PAGE_POST key: "post", post: v.post_key
                when "search" then POSTS_SEARCH key: "posts"
                else POSTS key: "posts"
            

    dom.BIG_MULTIGRAM = ->
        c = fetch "/current_user"
        v = fetch "view"

        username = v.user_key ? c?.user?.key ? "/user/default" 
        params = 
            computed: true
            tag: v.tag
            untagged: !v.tag
        w = inner_width

        DIV
            width: w
            className: "mobile-hide"

            MULTIGRAM
                key: "multigram-inner"
                sldr: "#{username}/votes/people#{stringify_kson params}"
                width: w
                height: 130
                max_avatar_radius: 60
                read_only: (v.user_key) or !c.logged_in
                onsave: (vote) ->
                    # Wait, why do we need to do this on a copy?
                    copy = Object.assign {}, vote
                    copy.key = "#{username}/vote/#{unslash vote.target_key}"
                    copy.depth = 1
                    if v.tag?
                        copy.key = "#{copy.key}(tag:#{v.tag})"
                        copy.tag = v.tag
                    save copy

    r = () ->
        # Set up the path parser for some local state
        parser = PPPParser bus
        parser("weights/user/<userid>").to_fetch = (key, t) ->
            {userid} = t._path
            {tag, untagged} = t._params
            blob = stringify_kson {computed: true, tag: tag, untagged}

            votes = fetch "/user/#{userid}/votes/people#{blob}"
            weights_ret = {key: key}
            (votes.arr ? []).forEach (v) ->
                fetch v
                weights_ret[v.target_key] = 2 * v.value - 1


            if tag
                ((fetch "/user/#{userid}/votes/people(computed,untagged)").arr ? []).forEach (v) ->
                    unless weights_ret[v.target_key]?
                        fetch v
                        weights_ret[v.target_key] = 2 * v.value - 1

            weights_ret

        parser("score/post/<postid>").to_fetch = (key, t) ->
            {postid} = t._path
            {user, tag} = t._params

            user ?= "/user/default"

            weights = fetch "weights/#{unslash user}#{stringify_kson {tag}}"
                
            now = Date.now() / 1000

            kson = stringify_kson
                tag: tag
                untagged: !tag
        
            #// Subscribe to the post
            p = fetch "/post/#{postid}"

            sum_votes = 0
            sum_weights = 0

            #// Subscribe to the post's votes
            (fetch "/votes/#{unslash p.key}#{kson}")?.arr?.forEach (v) ->
                # first subscribe to the vote
                if v.key then fetch v
                voter = v.user_key
                # Keep track of the total weight of votes, and of the weighted sum of votes.
                sum_weights += Math.abs(weights[voter] ? 0)
                sum_votes   += (2 * v.value - 1) * (weights[voter] ? 0)

            #// Our network-weight on the author
            author_weight = weights[p.user_key] ? 0

            res = compute_score
                age: now - p.time
                author: author_weight
                score: sum_votes
                volume: sum_weights

            {
                key: key,
                value: res
            }

        # discussion threading layout calculation
        parser("post_layout").to_fetch = (key, t) ->
            kson = stringify_kson t._params
            user = t._params.user
            min_score = (fetch user)?.filter ? 0.2
            posts = fetch "/posts"
            
            # assemble posts into an array of trees
            # also mark which posts are good
            children_of = root: []
            is_good = {}
            posts.arr?.forEach (post) ->
                parent = post.parent_key ? "root"
                (children_of[parent] ?= []).push post

                is_good[post.key] = (fetch "score#{post.key}#{kson}").value > min_score

            # assemble trees of posts into trees of collapsed good blocks
            blocks = root: {chain: null, context: null, end: 'root', children: []}
            # TODO: Handle the special case where a bad post interrupts a long string of good posts
            f_chain = (post, block, parent, skipped) ->
                # we might have to start a new block
                block ?= chain: [], context: post.parent_key, children: [], skipped: skipped
                if is_good[post.key]
                    block.chain.push post.key

                good_children = (children_of[post.key] ? [])
                    .filter (c) -> is_good[c.key]
                # if we only have one good child, continue the block
                if good_children.length == 1
                    f_chain good_children[0], block, parent, skipped
                # otherwise, end the block and then tell all the children to start new blocks
                else
                    new_parent = parent
                    new_skipped = skipped + 1
                    if block.chain?.length
                        # add the block to blocks and children
                        block.end = post.key
                        blocks[block.end] = block
                        blocks[parent].children.push block.end
                        # children will create blocks that are children of this block
                        new_parent = block.end
                        # nothing got skipped
                        new_skipped = 0
                    # chilren will need to start new blocks as children of the new parent
                    good_children.forEach (c) -> f_chain c, null, new_parent, new_skipped

            children_of.root.forEach (post) -> f_chain post, null, 'root', 0

            # TODO: dont just sort blocks by raw score
            # ie, give new content a bonus

            aggregate_score = (block) ->
                sum = block.chain
                    .map (c) -> 
                        val = (fetch "score#{c}#{kson}").value
                        # nonlinearity means that negative scores arent counted as heavily
                        # but might be more interesting to do something like E[exp(v(c))]
                        Math.max val, -Math.sqrt Math.abs val
                    .reduce (a, b) -> a + b
                
                sum / block.chain.length

            # flatten the blocks
            blocks_arr = []
            f_flatten = (block, level) ->
                block.level = level
                blocks_arr.push block
                # sort the children to flatten in the right order
                block.children
                    .map (c) -> blocks[c]
                    .sort (a, b) -> (aggregate_score b) - aggregate_score a
                    .forEach (c) -> f_flatten c, level + 1


            f_flatten blocks.root, -1
            # the first element is root, which isn't real
            blocks_arr.shift()

            {
                key: key,
                arr: blocks_arr
            }


    if document.readyState == "complete"
        r()
    else
        window.addEventListener("DOMContentLoaded", () => r())


</script>

<!-- Main statebus script -->
<script src="/client.js" server="/"></script>
<script src="/static/vendor/considerit_shared.js" crossorigin=anonymous></script>
<script src="/coffee/parser.coffee" crossorigin=anonymous></script>
<script src="/coffee/ui.coffee" crossorigin=anonymous></script>
<script src="/coffee/post.coffee" crossorigin=anonymous></script>

<!-- For slidergrams -->
<script src="/coffee/avatar.coffee" crossorigin=anonymous></script>
<script src="/coffee/slidergrams.coffee" crossorigin=anonymous></script>
<script src="/coffee/multigrams.coffee" crossorigin=anonymous></script>

<script src="https://unpkg.com/blueimp-md5@2.19.0/js/md5.js"></script>
<script src="/static/vendor/quadtree.js"></script>

<!-- Google Fonts: Lato -->
<link rel="preconnect" href="https://fonts.googleapis.com">
<link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
<link href="https://fonts.googleapis.com/css2?family=Lato:wght@300;400;700&display=swap" rel="stylesheet">
<link href="https://fonts.googleapis.com/icon?family=Material+Icons+Outlined" rel="stylesheet">

<!-- Favicon -->
<link rel="icon" type="image/svg+xml" href="/static/favicon.svg" sizes="any">
</head>
</html>
